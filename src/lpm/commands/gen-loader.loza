#
# gen-loader.loza
#
# The LPM Project
# Copyright 2024 Wsoum <wosoum@gmail.com>,
# Copyright 2021 Parham development team,
#           2021 parsa shahmaleki <parsampsh@gmail.com>
#
# This file is part of LPM.
#
# LPM is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# LPM is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with Loza.  If not, see <https://www.gnu.org/licenses/>.
#########################################################################

import_once $__dir__ + '/../pfile.loza'
import_once $__dir__ + '/../log.loza'

ns lpm; ns commands

@doc """\
Generates loza_modules/load.loza script\n\
Usage: lpm gen-loader [options...]\n\
"""
func gen_loader_cmd($args)
    $result = $args->validate({
    })
    if $result is not true
        lpm.print_warn 'lpm: gen-loader: ' + $result + '\n'
        return 1
    endif

    # load the package directory
    $pkg_dir = lpm.PackageDirectory()

    $all_paths = []

    $i = 0
    $list = os.listdir($pkg_dir->path)
    while $i < len($list)
        if os.path.isdir($pkg_dir->path + '/' + $list[$i]) and $list[$i] != '__lozaam__'
            # load paths of package
            $path = lpm.LPMFile->load_f($pkg_dir->path + '/' + $list[$i] + '/LPMfile')->path

            $j = 0
            while $j < len($path)
                $path[$j] = os.path.abspath($pkg_dir->path + '/' + $list[$i] + '/' + $path[$j])

                $j = $j + 1
            endwhile

            $all_paths = [*$all_paths, *$path]
        endif

        $i = $i + 1
    endwhile

    $loza_content = '$all_paths = ' + repr($all_paths) + '; python("os.environ[\'LOZAPATH\'] = \';\'.join(self.get_var(\'all_paths\')); self.bootstrap_modules()"); free("all_paths")'

    $f = fopen($pkg_dir->path + '/load.loza', 'w')
    $f->write($loza_content)
    $f->close()

    lpm.print_info 'Loader `' + $pkg_dir->path + '/load.loza' + '` Generated successfully.\n'

    return 0
endfunc

endns; endns
